buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'net.researchgate:gradle-release:2.3.0'
    }
}

group 'com.github.kennedyoliveira'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

// Custom configuration for intellij annotations
configurations {
    provided {
        description = 'Provided scope for the main SourceSet'
    }
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    provided 'org.jetbrains:annotations:15.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

// Custom IntelliJ configuration
idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]

        downloadJavadoc = true
        downloadSources = true
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Pastebin4j",
                "Implementation-Version": version,
                "Implementation-Vendor": "Kennedy Oliveira"
        )
    }
}

//
// Tests configuration
//

// Especial properties for running tests
test {
    systemProperty 'pastebin4j.devkey', System.getProperty('pastebin4j.devkey')
    systemProperty 'pastebin4j.username', System.getProperty('pastebin4j.username')
    systemProperty 'pastebin4j.password', System.getProperty('pastebin4j.password')

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
//
// Release information
//

modifyPom {
    project {
        name 'Pastebin4j'
        description 'Implementation of the PasteBin public API..'
        url 'https://github.com/kennedyoliveira/pastebin4j'
        inceptionYear '2015'

        scm {
            url 'https://github.com/kennedyoliveira/pastebin4j'
            connection 'scm:https://kennedyoliveira@github.com/kennedyoliveira/pastebin4j.git'
            developerConnection 'scm:git://github.com/kennedyoliveira/pastebin4j.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kennedyoliveira'
                name 'Kennedy Oliveira'
                email 'kennedy.oliveira@outlook.com'
                roles {
                    role 'administrator'
                    role 'developer'
                    role 'owner'
                }
            }
        }
    }
}

nexus {
    sign = true
}